buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.3'
    }
}

version = "0.5"
group = "com.monochromeroad.gradle-plugins"

apply plugin: "groovy"
apply plugin: 'maven-publish'
apply plugin: "bintray"

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile "net.java.dev.jets3t:synchronize:0.9.0"
    testCompile "org.spockframework:spock-core:0.6-groovy-1.8"
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = "sources"
}
jar.dependsOn sourcesJar

final pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "literalice"
            name "Masatoshi Hayashi"
            email "literalice@monochromeroad.com"
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar

            pom.withXml {
                def root = asNode()
                root.appendNode("description", "The gradle task for synchronizing a local directory with a AWS S3 bucket.")
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = bintrayUser
    key = bintrayKey
    publications = ["mavenJava"]
    pkg {
        repo = "maven"
        name = "gradle-aws-s3-sync"
        desc = "The gradle task for synchronizing a local directory with a AWS S3 bucket."
        licenses = ["Apache-2.0"]
        labels = ["gradle", "aws", "s3"]
    }
}

